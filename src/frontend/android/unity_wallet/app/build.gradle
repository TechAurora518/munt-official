apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        all {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
    flavorDimensions "default"
    productFlavors {
        gulden_testnet {
            applicationId 'com.gulden.testnet'
            archivesBaseName = 'gulden'
            resValue "string", "app_name", "Gulden-testnet"
            buildConfigField "boolean", "TESTNET", "true"
            buildConfigField "String", "OLD_WALLET_PROTOBUF_FILENAME", '"wallet-protobuf-testnet"'
            versionCode 342
            versionName = "2.0.1.0"
        }
        gulden_mainnet {
            applicationId 'com.gulden.wallet'
            archivesBaseName = 'gulden'
            resValue "string", "app_name", "Gulden"
            buildConfigField "boolean", "TESTNET", "false"
            buildConfigField "String", "OLD_WALLET_PROTOBUF_FILENAME", '"wallet-protobuf"'
            versionCode 342
            versionName = "2.0.1.0"
        }
    }
    // Build a separate apk for each native architecture we support.
    splits
    {
        abi
        {
            enable true
            reset()
            include "x86", "armeabi-v7a", "armeabi-v8a", "x86_64"
            universalApk false
        }
    }
}

configurations {
    // workaround bug in guava, see below in dependencies
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.material:material:1.1.0-alpha03'
    implementation 'androidx.preference:preference:1.1.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.gms:play-services-vision:17.0.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation('android.arch.work:work-runtime-ktx:1.0.0-beta03') { exclude group: 'com.google.guava', module: 'listenablefuture' }
    implementation "commons-validator:commons-validator:1.6" exclude module: "commons-logging"
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"
    //    implementation "org.jetbrains.anko:anko-sdk15:$anko_version" // sdk15, sdk19, sdk21, sdk23 and more are also available, pick highest one which is at most minSdkVersion
    //    implementation "org.jetbrains.anko:anko-appcompat-v7:$anko_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.8.0"
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    // begin wallet-protobuf dependencies for handling old wallet upgrade (essentially integrating GuldenJ)
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    implementation files(properties.getProperty('guldenj.dir') + '/core/target/guldenj-core-0.15-SNAPSHOT.jar')
    implementation 'com.google.protobuf:protobuf-java:3.0.0'
    implementation 'com.google.guava:guava:23.6-android' // don't upgrade to latest version of guava (it will fix the above 'listenablefuture' workaround, but it will require at least Android N)
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation('com.github.tony19:logback-android-classic:1.1.1-3') {
        exclude(group: 'com.google.android', module: 'android')
    }
    implementation 'com.madgag.spongycastle:core:1.51.0.0'
    implementation 'org.bitcoinj:orchid:1.1.1'
    implementation 'com.lambdaworks:scrypt:1.4.0'
    // end wallet-protobuf dependencies
}

// Map for the version code that gives each ABI a value.
ext.abiCodes = ['armeabi-v7a':1, 'x86':2, 'x86_64':3, 'armeabi-v8a':4]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    // Assigns a different version code for each output APK
    // other than the universal APK.
    variant.outputs.each { output ->
        // Stores the value of ext.abiCodes that is associated with the ABI for this variant.
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))

        if (baseAbiVersionCode != null)
        {
            output.versionCodeOverride = baseAbiVersionCode * 100000 + variant.versionCode
        }
    }
}

