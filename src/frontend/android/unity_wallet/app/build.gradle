plugins {
    id "com.jaredsburrows.license" version "0.8.42"
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    //This file can't (and shouldn't ever) be compressed.
    aaptOptions {
        noCompress 'staticfiltercp'
        noCompress 'staticfiltercptestnet'
    }

    //Latest okhttp requires java 8 minimum.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    buildTypes {
        all {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            shrinkResources false
            minifyEnabled false
            debuggable true
        }
    }

    // Flavour specific config.
    flavorDimensions "default"
    productFlavors {
        gulden_testnet {
            applicationId 'com.gulden.testnet'
            archivesBaseName = 'gulden'
            resValue "string", "app_name", "Gulden-testnet"
            buildConfigField "boolean", "TESTNET", "true"
            buildConfigField "String", "OLD_WALLET_PROTOBUF_FILENAME", '"wallet-protobuf-testnet"'
            versionCode 300378
            versionName = "2.1.11"
            resValue "string", "about_text_app_name", "Gulden testnet $versionName ($versionCode)"
            resValue "string", "about_text_app_copyright", "© 2019 Gulden.com BV"
        }

        gulden_mainnet {
            applicationId 'com.gulden.wallet'
            archivesBaseName = 'gulden'
            resValue "string", "app_name", "Gulden"
            buildConfigField "boolean", "TESTNET", "false"
            buildConfigField "String", "OLD_WALLET_PROTOBUF_FILENAME", '"wallet-protobuf"'
            versionCode 374
            versionName = "2.1.11"
            resValue "string", "about_text_app_name", "Gulden $versionName ($versionCode)"
            resValue "string", "about_text_app_copyright", "© 2019 Gulden.com BV"
        }
    }

    sourceSets {
        gulden_testnet {
            assets.srcDirs = ['src/main/assets', 'src/main/assetsTestnet']
        }
        gulden_mainnet {
            assets.srcDirs = ['src/main/assets', 'src/main/assetsMainnet']
        }
    }

    // Build a separate apk for each native architecture we support.
    splits
    {
        abi
        {
            enable true
            reset()
            include "x86", "armeabi-v7a", "arm64-v8a", "x86_64"
            universalApk false
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'androidx.versionedparcelable:versionedparcelable:1.1.0-alpha02'
    implementation 'androidx.preference:preference:1.1.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha5'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.gms:play-services-vision:17.0.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation('android.arch.work:work-runtime-ktx:1.0.0-beta03') { exclude group: 'com.google.guava', module: 'listenablefuture' }
    implementation "commons-validator:commons-validator:1.6" exclude module: "commons-logging"
    implementation "commons-io:commons-io:2.6"
    implementation 'com.squareup.okhttp3:okhttp:3.14.0'
    implementation 'com.itkacher.okhttpprofiler:okhttpprofiler:1.0.5'
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'se.ansman.kotshi:api:2.0-rc1'
    implementation 'com.jayway.jsonpath:json-path:2.4.0'
    kapt 'se.ansman.kotshi:compiler:2.0-rc1'
    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1' exclude group: 'com.google.code.findbugs', module: 'jsr305'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    //implementation "com.getkeepsafe.relinker:relinker:1.3.1"
}

licenseReport {
    generateHtmlReport = true
    generateJsonReport = true

    // These options are ignored for Java projects
    copyHtmlReportToAssets = true
    copyJsonReportToAssets = true
}
