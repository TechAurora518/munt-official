plugins {
    id "com.jaredsburrows.license" version "0.8.42"
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 32
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        ndk {
            debugSymbolLevel 'FULL'
        }
        ndkPath "$projectDir/../../../../../developer-tools/android-ndk/android-ndk-r23b/"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
    }

    //This file can't (and shouldn't ever) be compressed.
    aaptOptions {
        noCompress 'staticfiltercp'
    }

    //Latest okhttp requires java 8 minimum.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    buildTypes {
        all {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            shrinkResources false
            minifyEnabled false
            debuggable true
        }
    }

    // Flavour specific config.
    flavorDimensions "default"
    productFlavors {
        munt_testnet {
            applicationId 'com.gulden.testnet'
            archivesBaseName = 'munt'
            resValue "string", "app_name", "Munt-testnet"
            buildConfigField "boolean", "TESTNET", "true"
            buildConfigField "String", "OLD_WALLET_PROTOBUF_FILENAME", '"wallet-protobuf-testnet"'
            versionCode 300503
            versionName = "3.0.2"
            resValue "string", "about_text_app_name", "Munt testnet $versionName ($versionCode)"
            resValue "string", "about_text_app_copyright", "© 2022 Centure.com BV"
        }

        munt_mainnet {
            applicationId 'com.gulden.wallet'
            archivesBaseName = 'munt'
            resValue "string", "app_name", "Munt"
            buildConfigField "boolean", "TESTNET", "false"
            buildConfigField "String", "OLD_WALLET_PROTOBUF_FILENAME", '"wallet-protobuf"'
            versionCode 503
            versionName = "3.0.2"
            resValue "string", "about_text_app_name", "Munt $versionName ($versionCode)"
            resValue "string", "about_text_app_copyright", "© 2022 Centure.com BV"
        }
    }

    sourceSets {
        munt_testnet {
            assets.srcDirs = ['src/main/assets', 'src/main/assetsTestnet']
        }
        munt_mainnet {
            assets.srcDirs = ['src/main/assets', 'src/main/assetsMainnet']
        }
    }

    // Build a separate apk for each native architecture we support.
    splits
    {
        abi
        {
            enable true
            reset()
            include "x86", "armeabi-v7a", "arm64-v8a", "x86_64"
            universalApk false
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.google.android.material:material:1.8.0-alpha01'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'androidx.versionedparcelable:versionedparcelable:1.1.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0-beta01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.work:work-runtime:2.7.1"
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation "androidx.concurrent:concurrent-futures:1.1.0"
    implementation "androidx.concurrent:concurrent-futures-ktx:1.1.0"
    implementation "commons-validator:commons-validator:1.6" exclude module: "commons-logging"
    implementation "commons-io:commons-io:2.6"
    implementation 'com.squareup.okhttp3:okhttp:4.2.1'
    implementation 'com.localebro:okhttpprofiler:1.0.8'
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'se.ansman.kotshi:api:2.0-rc1'
    implementation 'com.jayway.jsonpath:json-path:2.4.0'
    kapt 'se.ansman.kotshi:compiler:2.0-rc1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1' exclude group: 'com.google.code.findbugs', module: 'jsr305'
    //todo: get rid of this ASAP
    implementation 'com.github.alvinhkh:TextDrawable:f9f516c43b'
    implementation 'com.android.volley:volley:1.2.1'
}

licenseReport {
    generateHtmlReport = true
    generateJsonReport = true

    // These options are ignored for Java projects
    copyHtmlReportToAssets = true
    copyJsonReportToAssets = true
}
