apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        all {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
    flavorDimensions "default"
    productFlavors {
        gulden_testnet {
            applicationId 'com.gulden.testnet'
            archivesBaseName = 'gulden'
            resValue "string", "app_name", "Gulden-testnet"
            buildConfigField "boolean", "TESTNET", "true"
            versionCode 333
            versionName = "2.0.1.0"
        }
        gulden_mainnet {
            applicationId 'com.gulden.unity_wallet'
            archivesBaseName = 'gulden'
            resValue "string", "app_name", "Gulden"
            buildConfigField "boolean", "TESTNET", "false"
            versionCode 333
            versionName = "2.0.1.0"
        }
    }

    // Build a seperate apk for each native architecture we support.
    splits
    {
        abi
        {
            enable true
            reset()
            include "x86", "armeabi-v7a", "armeabi-v8a", "x86_64"
            universalApk false
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.preference:preference:1.1.0-alpha01'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.gms:play-services-vision:16.2.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    implementation "android.arch.work:work-runtime-ktx:1.0.0-alpha11"
    implementation "commons-validator:commons-validator:1.6"
    implementation "com.squareup.okhttp3:okhttp:3.12.0"
    implementation "com.squareup.moshi:moshi:1.8.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.8.0"
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1-beta01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1-beta01'
}


// Map for the version code that gives each ABI a value.
ext.abiCodes = ['armeabi-v7a':1, 'x86':2, 'x86_64':3, 'armeabi-v8a':4]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    // Assigns a different version code for each output APK
    // other than the universal APK.
    variant.outputs.each { output ->
        // Stores the value of ext.abiCodes that is associated with the ABI for this variant.
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))

        if (baseAbiVersionCode != null)
        {
            output.versionCodeOverride = baseAbiVersionCode * 100000 + variant.versionCode
        }
    }
}

